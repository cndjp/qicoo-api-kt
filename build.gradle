buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_version"
    }
}

//plugins {
//    id 'nu.studer.jooq' version '3.0.2'
//}

plugins {
    id 'com.google.cloud.tools.jib' version '1.7.0'
    id 'com.palantir.git-version' version '0.12.2'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: "org.jlleitschuh.gradle.ktlint"

group 'main.kotlin.cndjp.qicoo.api'
version gitVersion()
mainClassName = "io.ktor.server.netty.EngineMain"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src/main/kotlin']
    test.kotlin.srcDirs = test.java.srcDirs = ['src/test/kotlin']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/spekframework/spek" }
    maven { url = 'https://dl.bintray.com/michaelbull/maven' }
}

configurations {
    ktlint
}

dependencies {
    // stdlib
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // kotlin-result
    implementation "com.michael-bull.kotlin-result:kotlin-result:1.1.0"
    // gson
    implementation 'com.google.code.gson:gson:2.8.6'
    // lint
    ktlint "com.github.shyiko:ktlint:0.30.0"
    // ktor
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    // log
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation 'io.github.microutils:kotlin-logging:1.7.6'
    // redis
    implementation "redis.clients:jedis:$jedis_version"
    // rdb
    implementation "org.jetbrains.exposed:exposed:$exposed_version"
    implementation "mysql:mysql-connector-java:$mysql_version"
    // di
    implementation "org.kodein.di:kodein-di-generic-jvm:$kodein_version"
        //jooqRuntime group: 'mysql', name: 'mysql-connector-java', version: "$mysql_version"
        //compile group: 'org.jooq', name: 'jooq', version: "$jooq_version"
        //compile group: 'org.jooq', name: 'jooq-parent', version: "$jooq_version"/*, ext: 'pom'*/
        //compile group: 'org.jooq', name: 'jooq-meta', version: "$jooq_version"
        //compile group: 'org.jooq', name: 'jooq-meta-extensions', version: "$jooq_version"
        //compile group: 'org.jooq', name: 'jooq-codegen', version: "$jooq_version"
        //compile group: 'org.jooq', name: 'jooq-codegen-maven', version: "$jooq_version"
    // test
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

shadowJar {
    archivesBaseName = 'qicoo'
    archiveVersion = ""
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.3"
        languageVersion = "1.3"
    }
}

task runMigrate(type: JavaExec) {
    main ='test.cndjp.qicoo.domain.repository.support.migration_run.migrate.MainKt'
    classpath = sourceSets.test.runtimeClasspath
}

task runClean(type: JavaExec) {
    main ='test.cndjp.qicoo.domain.repository.support.migration_run.clean.MainKt'
    classpath = sourceSets.test.runtimeClasspath
}

jib {
    def details = versionDetails()
    from {
        image = 'docker://cndjp/qicoo-api-kt:base'
    }
    to {
        image = 'docker.io/cndjp/qicoo-api-kt'
        tags = [details.gitHashFull, 'latest']
    }
    container {
        jvmFlags = ['-Xms512m', '-Xmx4096m', '-Xdebug']
        ports = ['8081/tcp']
        format = 'OCI'
    }
}